
buildscript {
    repositories {
        flatDir { dirs 'gradle/plugins' }
        maven {
            url 'http://storage.googleapis.com/r8-releases/raw'
        }
    }
    dependencies {
        // override the shadow plugin with a version that uses R8.
        classpath ':shadow:'
        classpath 'com.android.tools:r8:2.0.61'
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
    id 'com.github.kt3k.coveralls' version '2.8.4'
}

group 'org.netomi.uom'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

shadowJar {
    exclude 'META-INF/**'
    exclude '**/**.properties'
    exclude '**/*.swp'

    relocate 'com.google.common', 'org.netomi.uom.util.shadow.guava'
    relocate 'org.hibernate.validator.internal', 'org.netomi.uom.util.shadow.hibernate'
    minimize()
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

test {
    jacoco {
        excludes += ['org/netomi/uom/util/ConcurrentReferenceHashMap**']
    }
}

jacocoTestReport {
    reports {
        xml.enabled  = true
        html.enabled = true
    }

    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: '**/ConcurrentReferenceHashMap**')
        }))
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile('com.google.guava:guava:28.2-jre')
    compile('org.hibernate.validator:hibernate-validator:6.1.3.Final')

    testImplementation('org.junit.jupiter:junit-jupiter:5.6.1')
    testImplementation('com.google.guava:guava-testlib:28.2-jre')
    testImplementation('org.assertj:assertj-core:3.15.0')
}
